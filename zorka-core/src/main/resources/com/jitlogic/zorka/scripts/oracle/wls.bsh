/**
 * Copyright 2012-2019 Rafal Lewczuk <rafal.lewczuk@jitlogic.com>
 *
 * ZORKA is free software. You can redistribute it and/or modify it under the
 * terms of the GNU General Public License as published by the Free Software
 * Foundation, either version 3 of the License, or (at your option) any later
 * version.
 *
 * ZORKA is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE. See the GNU General Public License for more
 * details.
 *
 * You should have received a copy of the GNU General Public License along with
 * ZORKA. If not, see <http://www.gnu.org/licenses/>.
 */

zorka.require(
  "ejb.bsh",
  "jvm.bsh",
  "http.bsh",
  "javax/jms.bsh",
  "apache/cxf.bsh"
);

__wls() {


  if (zorka.boolCfg("http.instrument.wls", true))
  spy.add(spy.instrument("WLS_HTTP")
    .onEnter(spy.fetchArg("REQ", 1), spy.fetchArg("RESP", 2),
      spy.format("URI", "${REQ.requestURI}"),
      http._trace ? spy.subchain(
        http._uriQualifier,
        tracer.begin("http", zorka.longCfg("http.trace.time")),
        tracer.attr("http.url", "URI"),
        tracer.formatAttr("http.method", "${REQ.method}"),
        http._headers ? http.headers_processor(true, false) : null,
        (http._trace && _dtrace_enabled && http._headers) ? tracer.dtraceInput(_dtrace_input_flags,tracer.DFK_SERVER) : null
      ) : null,
      http._congestion ? spy.zorkaStats("java", http._mbean, "stats", "ALL", "T", null, spy.ACTION_ENTER) : null)
    .onError(spy.fetchError("EX"), spy.format("ERR", "${EX}"),
      http._trace ? spy.subchain(
        http._uriQualifier,
        tracer.flags(tracer.SUBMIT_TRACE),
        tracer.attr("jvm.exception", "ERR")
      ) : null)
    .onSubmit(
      spy.strTime("TIME"),
      spy.format("STATUS", "${RESP.status}"),
      spy.get("USER", "REQ", "remoteUser"),
      http._trace ? spy.subchain(
        http._uriQualifier,
        tracer.attr("http.status_code", "STATUS"),
        tracer.filterBy("STATUS", null, http._errors, null, null),
        tracer.attr("http.user", "USER"),
        http._params ? http.param_processor() : null,
        http._cookies ? http.cookies_processor(true, false) : null,
        (zorka.boolCfg("wls.headers.out", true) && http._headers) ? http.headers_processor(false, true) : null
      ) : null,
      http._slow_chain,
      http._error ? spy.subchain(
        spy.valSetFilter("STATUS", http._errors),
        spy.markError(),
        http._error_chain) : null,
      http._stats ? spy.zorkaStats("java", http._mbean, "stats", "ALL", "T", null, http._action) : null,
      http._tags_chain)
    .include(
      spy.byMethod(0, "weblogic.servlet.internal.WebAppServletContext", "execute", null)
    ));


    spy.add(spy.instrument("WLS_EJB")
      .onEnter(
          spy.fetchArg("IWRAP", 2), spy.fetchArg("ARGS", 3),
          ejb._trace ? tracer.begin("ejb", zorka.longCfg("ejb.trace.time")) : null,
          ejb._trace ? tracer.formatAttr("ejb.method", "${IWRAP.methodDescriptor.method.name}") : null,
          ejb._trace ? tracer.formatAttr("ejb.class", "${IWRAP.methodDescriptor.method.declaringClass.name}") : null)
      .onError(
          ejb._trace ? tracer.flags(tracer.SUBMIT_TRACE) : null,
          spy.fetchError("ERR"),
          spy.format("MSG", "${ERR.message}"),
          spy.strTime("TIME"),
          ejb._error_chain)
      .onReturn(
          ejb._params ? spy.fetchRetVal("RET") : spy.put("RET", ""))
      .onSubmit(
          ejb._params ? (com.jitlogic.zorka.core.spy.SpyProcessor)(ejb.arg_processor()) : spy.put("PARAMS", ""),
          ejb._trace ? tracer.attr("ejb.result", "RET") : null,
          ejb._stats ? spy.zorkaStats("java", ejb._mbean, "byMethod", "${CLASS}.${METHOD}") : null,
          ejb._stats ? spy.zorkaStats("java", ejb._mbean, "stats", "ALL") : null,
          ejb._tags_chain,
          spy.longerThan(zorka.longCfg("ejb.slow.time")),
          spy.strTime("TIME"),
          ejb._slow_chain)
      .include(
          spy.byMethod(0, "weblogic.ejb.container.internal.SessionLocalMethodInvoker", "invoke", null),
          spy.byMethod(0, "weblogic.ejb.container.internal.SessionRemoteMethodInvoker", "invoke", null)
      ));

  if (zorka.boolCfg("zabbix.active")) {
    zorka.require("zabbix.bsh");
    zabbix.tagMetadata("WEBLOGIC");
  }

  if (zorka.boolCfg("wls.mbs.autoregister", true)) {
    jvm.autoregister();
  }

  return this;
}

wls = __wls();

